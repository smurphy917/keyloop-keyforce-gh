IF(
    OR(
        ISPICKVAL(SBQQ__DiscountScheduleType__c, 'Slab'),
        ISPICKVAL(SBQQ__PricingMethod__c, 'Block')
    ),
    IF(
        OR(
            AND(
                !SBQQ__Existing__c, 
                !SBQQ__CarryoverLine__c, 
                SBQQ__Quantity__c == 0
            ),
            AND(
                OR(
                    SBQQ__Existing__c, 
                    SBQQ__CarryoverLine__c
                ),
                OR(
                    SBQQ__Quantity__c == SBQQ__PriorQuantity__c - SBQQ__UpgradedQuantity__c,
                    AND(
                        !SBQQ__AllowAssetRefund__c, 
                        ISPICKVAL(SBQQ__SubscriptionPricing__c, ''), 
                        SBQQ__Quantity__c < SBQQ__PriorQuantity__c - SBQQ__UpgradedQuantity__c
                    )
                )
            )
        ),
        0,
        1
    ),
    IF(
        AND(
            !SBQQ__Existing__c, 
            !SBQQ__CarryoverLine__c
        ),
        SBQQ__Quantity__c,
        IF(
            SBQQ__Quantity__c >= SBQQ__PriorQuantity__c - SBQQ__UpgradedQuantity__c,
            IF(
                ISPICKVAL(SBQQ__SubscriptionPricing__c, 'Percent Of Total'),
                SBQQ__Quantity__c,
                SBQQ__Quantity__c - SBQQ__PriorQuantity__c + SBQQ__UpgradedQuantity__c
            ),
            IF(
                AND(
                    !SBQQ__AllowAssetRefund__c, 
                    ISPICKVAL(SBQQ__SubscriptionPricing__c, '')
                ),
                0,
                SBQQ__Quantity__c - SBQQ__PriorQuantity__c + SBQQ__UpgradedQuantity__c
            )
        )
    )
)